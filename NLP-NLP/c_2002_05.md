[Nonlinear-Nonlinear](/BASBLib/NLP-NLP-problems) bilevel problem from [(Colson, 2002)][Colson, 2002]

---

![](/BASBLib/images/c_2002_05_eq.jpg)

## Optimal solution

Objective values   | Solution point           |
------------------ | ------------------------ |
F* = 2.750         | _x_* = 1.941             |
f* = 0.548         | __y__* = (0.000, 1.211)  |

## Sources where this problem occurs

__Original source:__

 - _BIPA5_ in [(Colson, 2002)][Colson, 2002]

__Other sources:__

 - _BIPA5_ in [(Colson et al., 2005)][Colson et al., 2005]

## Description of the problem in the `AMPL` format

```ruby
set J := {1..2};
param ylb{J};                        # Lower Bounds for the inner variable
param yub{J};                        # Upper Bounds for the inner variable
var x >= 0, <= 10;                   # Outer variable
var y{j in J} >= ylb[j], <= yub[j];  # Inner variable
var l{1..6} >= 0, <= 100;            # KKT Multipliers

minimize outer_obj: (x - y[2])^4 + (y[1] - 1)^2 + (y[1] - y[2])^2;  # Outer objective

subject to
# Inner objective:
    inner_obj: 2*x + exp(y[1]) + y[1]^2 + 4*y[1] + 2*y[2]^2 - 6*y[2] = 0;
# Inner constraints
    inner_con_1:    6*x + y[1]^2 + exp(y[2]) - 15 <= 0;
    inner_con_2:    5*x + y[1]^4 - y[2] - 25 <= 0;
# KKT conditions:
    stationarity_1:    exp(y[1]) + 2*y[1] + 4 + 2*l[1]*y[1] + 4*l[2]*y[1]^3 - l[3] + l[4] = 0;
    stationarity_2:    4*y[2] - 6 + l[1]*exp(y[2]) - l[2] - l[5] + l[6] = 0;
    complementarity_1: l[1]*(6*x + y[1]^2 + exp(y[2]) - 15) = 0;
    complementarity_2: l[2]*(5*x + y[1]^4 - y[2] - 25) = 0;
    complementarity_3: l[3]*y[1] = 0;
    complementarity_4: l[4]*(y[1] - 4) = 0;
    complementarity_5: l[5]*y[2] = 0;
    complementarity_6: l[6]*(y[2] - 2) = 0;

# Data for parameter bounds
data;
param ylb :=
    1  0
    2  0
;
param yub :=
    1  4
    2  2
;
```

##  References

 - [B. Colson, P. Marcotte, and G. Savard, A trust-region method for nonlinear bilevel programming: algorithm and computational experience, Computational Optimization and Applications, 30 (2005), pp. 211â€“227](https://doi.org/10.1007/s10589-005-4612-4)
 - [B. Colson, BIPA(BIlevel Programming with Approximation Methods)(software guide and test problems), Cahiers du GERAD, (2002)](https://www.gerad.ca/en/papers/G-2002-37/view)

[<img src="http://www.interupgrade.com/images/pfeil-backbutton.png" width="40" height="40">](/BASBLib/NLP-NLP-problems "Back to summary of NLP-NLP bilevel problems")

[Colson, 2002]: https://www.gerad.ca/en/papers/G-2002-37/view
[Colson et al., 2005]: https://doi.org/10.1007/s10589-005-4612-4