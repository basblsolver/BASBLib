[fz_1998_01](/BASBLib/NLP-NLP/fz_1998_01) : [Nonlinear-Nonlinear](/BASBLib/NLP-NLP-problems) bilevel problem from [(Floudas & Zlobec, 1998)][Floudas & Zlobec, 1998]

---

![](/BASBLib/images/fz_1998_01_eq.jpg)

## Optimal solution

Objective values   | Solution point          |
------------------ | ----------------------- |
F* = 1.0           | _x_* = 1.00             |
f* = -1.0          | __y__* = (0.0, 1.0)     |

Note that the inner problem is convex, and therefore KKT optimality conditions are necessary and sufficient.

## Sources where this problem occurs

__Original source:__

 - __4.1 Example__ in [(Floudas & Zlobec, 1998)][Floudas & Zlobec, 1998]

__Other sources:__

 - __Example 2__ in [(Gümüş & Floudas, 2001)][Gümüş & Floudas, 2001]
 - __Example 4.2 (gf\_2)__ in [(Mitsos & Barton, 2007)][Mitsos & Barton, 2007]
 - __gf\_2__ in [(Mitsos et al., 2008)][Mitsos et al., 2008]
 - __Example 5.9, Problem No. 4__ in [(Nie et al., 2017)][Nie et al., 2017]

## Description of the problem in the `AMPL` format

```ruby
set J := {1..2};
var x >= 0, <= 1;                 # Outer variable
param lb{J};                      # Lower Bounds for the inner variable
param ub{J};                      # Upper Bounds for the inner variable
var y{j in J} >= lb[j] <= ub[j];  # Inner variable
var l{1..6} >= 0, <= 10;          # KKT Multipliers

minimize outer_obj: x^3*y[1] + y[2];   # Outer objective

subject to
# Inner objective:
    inner_obj: -y[2] = 0;
# Inner constraints
    inner_con1: x*y[1] - 10 <= 0;
    inner_con2: y[1]^2 + x*y[2] -1 <= 0;
# KKT conditions:
    stationarity_1:    l[1]*x + 2*l[2]*y[1] - l[3] + l[4] = 0;
    stationarity_2:    -1 + l[2]*x - l[5] + l[6] = 0;
    complementarity_1: l[1]*(x*y[1] - 10) = 0;
    complementarity_2: l[2]*(y[1]^2 + x*y[2] -1) = 0;
    complementarity_3: l[3]*(-1 - y[1]) = 0;
    complementarity_4: l[4]*(y[1] - 1) = 0;
    complementarity_5: l[5]*y[2] = 0;
    complementarity_6: l[6]*(y[2] - 100) = 0;

# Data for parameter bounds
data;
param lb :=
    1 -1
    2  0
;
param ub :=
    1  1
    2  100
;
```

##  References

 - [C. A. Floudas and S. Zlobec, Optimality and Duality in Parametric Convex Lexicographic Programming, Springer US, Boston, MA, 1998, pp. 359–379](https://doi.org/10.1007/978-1-4613-0307-7_16)
 - [Z. H. Gümüş and C. A. Floudas, Global optimization of nonlinear bilevel programming problems, Journal of Global Optimization, 20 (2001), pp. 1–31](https://doi.org/10.1023/A:1011268113791)
 - [A. Mitsos and P. I. Barton, A Test Set for Bilevel Programs, 2007. (Last updated September 19, 2007)](https://www.researchgate.net/publication/228455291_A_test_set_for_bilevel_programs)
 - [A. Mitsos, P. Lemonidis, and P. I. Barton, Global solution of bilevel programs with a nonconvex inner program, Journal of Global Optimization, 42 (2008), pp. 475–513](https://doi.org/10.1007/s10898-007-9260-z)
 - [J. Nie, L. Wang, and J. Ye, Bilevel polynomial programs and semidefinite relaxation methods, SIAM Journal on Optimization, 27 (2017), pp. 1728–1757](https://doi.org/10.1137/15M1052172)

[<img src="http://www.interupgrade.com/images/pfeil-backbutton.png" width="40" height="40">](/BASBLib/NLP-NLP-problems "Back to summary of NLP-NLP bilevel problems")
[<img src="https://cdn1.iconfinder.com/data/icons/MetroStation-PNG/128/MB__home.png" width="40" height="40">](/BASBLib/index "Back to homepage")

[Floudas & Zlobec, 1998]: https://doi.org/10.1007/978-1-4613-0307-7_16
[Gümüş & Floudas, 2001]: https://doi.org/10.1023/A:1011268113791
[Mitsos & Barton, 2007]: https://www.researchgate.net/publication/228455291_A_test_set_for_bilevel_programs
[Mitsos et al., 2008]: https://doi.org/10.1007/s10898-007-9260-z
[Nie et al., 2017]: https://doi.org/10.1137/15M1052172
